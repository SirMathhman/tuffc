let {
    p_at, p_eat, p_expect, p_parse_identifier, p_at_kind, p_peek, p_parse_type,
    p_can_start_type_tok_at, p_error_with_token_context, node_kind, node_new, node_set_data1, node_set_data2, node_set_data3, node_set_data4, node_set_data5, node_get_data1, node_get_data2
}
 = selfhost::parser_core;
let {
    tok_kind, tok_value, get_interned_str, vec_new
}
 = selfhost::runtime_lexer;
fn p_has_generic_call_suffix() : Bool => {
    if (!p_at(TK_SYMBOL, "<")) {
        return false;
    }
    if (!p_can_start_type_tok_at(1)) {
        return false;
    }
    let cursor = 0;
    let depth = 0;
    while (true) {
        if (cursor > 200000) {
            p_error_with_token_context("Parser watchdog: generic-call suffix scan exceeded 200000 tokens");
        }
        let ti = p_peek(cursor);
        let tk = tok_kind(ti);
        if (tk == TK_EOF) {
            return false;
        }
        if (tk == TK_SYMBOL) {
            let sym = get_interned_str(tok_value(ti));
            if (sym.str_eq("<")) {
                depth = depth + 1;
            }
            else if (sym.str_eq(">")) {
                depth = depth - 1;
                if (depth == 0) {
                    let next = p_peek(cursor + 1);
                    if (tok_kind(next) == TK_SYMBOL) {
                        let next_sym = get_interned_str(tok_value(next));
                        return next_sym.str_eq("(");
                    }
                    return false;
                }
            }
        }
        cursor = cursor + 1;
    }
}
fn p_has_generic_struct_init_suffix() : Bool => {
    if (!p_at(TK_SYMBOL, "<")) {
        return false;
    }
    if (!p_can_start_type_tok_at(1)) {
        return false;
    }
    let cursor = 0;
    let depth = 0;
    while (true) {
        if (cursor > 200000) {
            p_error_with_token_context("Parser watchdog: generic-struct-init suffix scan exceeded 200000 tokens");
        }
        let ti = p_peek(cursor);
        let tk = tok_kind(ti);
        if (tk == TK_EOF) {
            return false;
        }
        if (tk == TK_SYMBOL) {
            let sym = get_interned_str(tok_value(ti));
            if (sym.str_eq("<")) {
                depth = depth + 1;
            }
            else if (sym.str_eq(">")) {
                depth = depth - 1;
                if (depth == 0) {
                    let next = p_peek(cursor + 1);
                    if (tok_kind(next) == TK_SYMBOL) {
                        let next_sym = get_interned_str(tok_value(next));
                        return next_sym.str_eq("{");
                    }
                    return false;
                }
            }
        }
        cursor = cursor + 1;
    }
}
fn p_has_generic_value_suffix() : Bool => {
    if (!p_at(TK_SYMBOL, "<")) {
        return false;
    }
    if (!p_can_start_type_tok_at(1)) {
        return false;
    }
    let cursor = 0;
    let depth = 0;
    while (true) {
        if (cursor > 200000) {
            p_error_with_token_context("Parser watchdog: generic-value suffix scan exceeded 200000 tokens");
        }
        let ti = p_peek(cursor);
        let tk = tok_kind(ti);
        if (tk == TK_EOF) {
            return false;
        }
        if (tk == TK_SYMBOL) {
            let sym = get_interned_str(tok_value(ti));
            if (sym.str_eq("<")) {
                depth = depth + 1;
            }
            else if (sym.str_eq(">")) {
                depth = depth - 1;
                if (depth == 0) {
                    let next = p_peek(cursor + 1);
                    if (tok_kind(next) == TK_SYMBOL) {
                        let next_sym = get_interned_str(tok_value(next));
                        if (next_sym.str_eq("(")) {
                            return false;
                        }
                    }
                    return true;
                }
            }
        }
        cursor = cursor + 1;
    }
}
fn p_parse_generic_arg_value_or_type() : I32 => {
    let t0 = p_peek(0);
    if (tok_kind(t0) == TK_IDENTIFIER && tok_kind(p_peek(1)) == TK_SYMBOL) {
        let s1 = get_interned_str(tok_value(p_peek(1)));
        if (s1.str_eq(".")) {
            return p_parse_expression(5);
        }
    }
    let cursor = 0;
    let paren_depth = 0;
    let bracket_depth = 0;
    while (true) {
        if (cursor > 200000) {
            p_error_with_token_context("Parser watchdog: generic-arg scan exceeded 200000 tokens");
        }
        let ti = p_peek(cursor);
        let tk = tok_kind(ti);
        if (tk == TK_EOF) {
            break;
        }
        if (tk == TK_SYMBOL) {
            let sym = get_interned_str(tok_value(ti));
            if (sym.str_eq("(")) {
                paren_depth = paren_depth + 1;
            }
            else if (sym.str_eq(")")) {
                if (paren_depth > 0) {
                    paren_depth = paren_depth - 1;
                }
            }
            else if (sym.str_eq("[")) {
                bracket_depth = bracket_depth + 1;
            }
            else if (sym.str_eq("]")) {
                if (bracket_depth > 0) {
                    bracket_depth = bracket_depth - 1;
                }
            }
            else if (paren_depth == 0 && bracket_depth == 0 && (sym.str_eq(",") || sym.str_eq(">"))) {
                break;
            }
            else if (
            paren_depth == 0 &&
            bracket_depth == 0 &&
            (
            (cursor > 0 && sym.str_eq(".")) ||
            (cursor > 0 && sym.str_eq("+")) ||
            (cursor > 0 && sym.str_eq("-")) ||
            (cursor > 0 && sym.str_eq("*")) ||
            (cursor > 0 && sym.str_eq("/")) ||
            (cursor > 0 && sym.str_eq("%"))
            )
            ) {
                return p_parse_expression(5);
            }
        }
        cursor = cursor + 1;
    }
    p_parse_type()
}
fn p_try_parse_into_generic_value_call(expr: I32) : I32 => {
    if (!(node_kind(expr) == NK_MEMBER_EXPR && get_interned_str(node_get_data2(expr)).str_eq("into") && p_at(TK_SYMBOL, "<") && p_has_generic_value_suffix())) {
        return 0;
    }
    p_eat();
    let type_args = vec_new();
    if (!p_at(TK_SYMBOL, ">")) {
        type_args.vec_push(p_parse_generic_arg_value_or_type());
        while (p_at(TK_SYMBOL, ",")) {
            p_eat();
            type_args.vec_push(p_parse_generic_arg_value_or_type());
        }
    }
    p_expect(TK_SYMBOL, ">", "Expected '>' after into type args");
    let recv = node_get_data1(expr);
    let prop = node_get_data2(expr);
    let args = vec_new();
    args.vec_push(recv);
    let callee = node_new(NK_IDENTIFIER);
    node_set_data1(callee, prop);
    let into_value = node_new(NK_CALL_EXPR);
    node_set_data1(into_value, callee);
    node_set_data2(into_value, args);
    node_set_data3(into_value, 1);
    node_set_data4(into_value, type_args);
    node_set_data5(into_value, 1);
    into_value
}
fn p_parse_call_args() : Vec<I32> => {
    let args = vec_new();
    if (!p_at(TK_SYMBOL, ")")) {
        args.vec_push(p_parse_expression(0));
        while (p_at(TK_SYMBOL, ",")) {
            p_eat();
            args.vec_push(p_parse_expression(0));
        }
    }
    args
}
fn p_lower_member_call(expr: I32, args: Vec<I32>, type_args: Vec<I32>, has_type_args: I32) : I32 => {
    let recv = node_get_data1(expr);
    let prop = node_get_data2(expr);
    let lowered_args = vec_new();
    lowered_args.vec_push(recv);
    let ai = 0;
    let alen = args.vec_length();
    while (ai < alen) {
        lowered_args.vec_push(args.vec_get(ai));
        ai = ai + 1;
    }
    let callee = node_new(NK_IDENTIFIER);
    node_set_data1(callee, prop);
    let call = node_new(NK_CALL_EXPR);
    node_set_data1(call, callee);
    node_set_data2(call, lowered_args);
    node_set_data3(call, 1);
    if (has_type_args == 1) {
        node_set_data4(call, type_args);
    }
    call
}
fn p_try_parse_generic_call(expr: I32) : I32 => {
    if (!(p_at(TK_SYMBOL, "<") && p_has_generic_call_suffix())) {
        return 0;
    }
    p_eat();
    let type_args = vec_new();
    if (!p_at(TK_SYMBOL, ">")) {
        type_args.vec_push(p_parse_generic_arg_value_or_type());
        while (p_at(TK_SYMBOL, ",")) {
            p_eat();
            type_args.vec_push(p_parse_generic_arg_value_or_type());
        }
    }
    p_expect(TK_SYMBOL, ">", "Expected '>' after generic call type args");
    p_expect(TK_SYMBOL, "(", "Expected '(' after generic call type args");
    let args = p_parse_call_args();
    p_expect(TK_SYMBOL, ")", "Expected ')' after call args");
    if (node_kind(expr) == NK_MEMBER_EXPR) {
        return p_lower_member_call(expr, args, type_args, 1);
    }
    let call = node_new(NK_CALL_EXPR);
    node_set_data1(call, expr);
    node_set_data2(call, args);
    node_set_data3(call, 0);
    node_set_data4(call, type_args);
    call
}
fn p_try_parse_call(expr: I32) : I32 => {
    if (!p_at(TK_SYMBOL, "(")) {
        return 0;
    }
    p_eat();
    let args = p_parse_call_args();
    p_expect(TK_SYMBOL, ")", "Expected ')'");
    if (node_kind(expr) == NK_MEMBER_EXPR) {
        return p_lower_member_call(expr, args, vec_new(), 0);
    }
    let call = node_new(NK_CALL_EXPR);
    node_set_data1(call, expr);
    node_set_data2(call, args);
    node_set_data3(call, 0);
    call
}
fn p_try_parse_member(expr: I32) : I32 => {
    if (!p_at(TK_SYMBOL, ".")) {
        return 0;
    }
    p_eat();
    let prop = 0;
    if (p_at_kind(TK_IDENTIFIER)) {
        prop = p_parse_identifier();
    }
    else if (p_at_kind(TK_KEYWORD)) {
        prop = tok_value(p_eat());
    }
    else {
        p_error_with_token_context("Expected member name after '.'");
    }
    let member = node_new(NK_MEMBER_EXPR);
    node_set_data1(member, expr);
    node_set_data2(member, prop);
    member
}
fn p_try_parse_index_or_unwrap(expr: I32) : I32 => {
    if (p_at(TK_SYMBOL, "[")) {
        p_eat();
        let idx_expr = p_parse_expression(0);
        p_expect(TK_SYMBOL, "]", "Expected ']'");
        let idx_node = node_new(NK_INDEX_EXPR);
        node_set_data1(idx_node, expr);
        node_set_data2(idx_node, idx_expr);
        return idx_node;
    }
    if (p_at(TK_SYMBOL, "?")) {
        p_eat();
        let unwrap = node_new(NK_UNWRAP_EXPR);
        node_set_data1(unwrap, expr);
        return unwrap;
    }
    0
}
out fn p_parse_postfix(exprIn: I32) : I32 => {
    let expr = exprIn;
    let postfix_watchdog = 0;
    while (true) {
        postfix_watchdog = postfix_watchdog + 1;
        if (postfix_watchdog > 10000) {
            p_error_with_token_context("Parser watchdog: postfix loop exceeded 10000 iterations");
        }
        let next = p_try_parse_into_generic_value_call(expr);
        if (next != 0) {
            expr = next;
            continue;
        }
        next = p_try_parse_generic_call(expr);
        if (next != 0) {
            expr = next;
            continue;
        }
        next = p_try_parse_call(expr);
        if (next != 0) {
            expr = next;
            continue;
        }
        next = p_try_parse_member(expr);
        if (next != 0) {
            expr = next;
            continue;
        }
        next = p_try_parse_index_or_unwrap(expr);
        if (next != 0) {
            expr = next;
            continue;
        }
        break;
    }
    expr
}
out fn selfhost_parser_expr_postfix_marker() : I32 => 0;
